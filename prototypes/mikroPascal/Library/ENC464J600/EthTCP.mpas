unit EthTCP;

{.$DEFINE LOG_UART}

uses


NMRAnetDefinesShared,

  EthCommon,
  EthSockets,
  dspic_additional_string_library,
  lib1_enc600_V3_5_b,
  definitions_ENC600;
  
function TCP_UserSend(var ip_t1 : TipHeader; dest_port_S1, len_data1 : word; var data_tcp2 : array[1472] of byte) : boolean;

// Internal functions
function TCP_Send_Internal(TCP_Flag_T : byte; iSocket: Integer; Fragment: Boolean) : boolean;
procedure TCP_WriteToWire_Internal(source_portT, dest_portT, PacketLenT : word; SEQ_NoT, ACK_NoT : dWord; TCP_FlagT : byte);
procedure TCP_Init_Internal;
procedure TCP_Process_Internal(EthMemAddressRequest: Word);

// External functions
procedure Eth_UserTCP(var dest_ip_addr_T : TipHeader; var source_port_T, dest_port_T, len_T : word; iSocket : Integer); external;
procedure Eth_DoPacket; external;

implementation

var
  SEQ_No, ACK_No, SEQ_NoR, ACK_NoR : dWord;
  TCP_IdentificationCounter: word;
  Window : word;
  
procedure TCP_Init_Internal;
begin
  TCP_IdentificationCounter := 1000;
  ACK_No := $42844A80;
  SEQ_No := $42844A80;
end;

//******************************************************************************
//* Name    : TCP_Send_Load_Addresses_And_Data_Internal
//* Purpose : Send TCP packets, used by TCP/IP Stack
//  Description: This is only called by TCP_Send_Internal to move the MAC and
//               IP addresses to the ENC device, then decode if the data is RAM
//               or ROM and move that to the device.  It then calls the function
//               to load the rest of the IP/TCP header info to the device and send
//               the packet
//******************************************************************************
function TCP_Send_LoadAddressesAndDataThenSend_Internal(TCP_Flag_T: byte; iSocket: byte; AddressOffset : word; MaxPacketDataSize: Word) : word;
var 
  BytesToWrite : word;
  Stop_addr_S, Start_addr_S : dword;
  kk : dword;
  ptf : ^far const byte;
  
begin
  Result := 0;
  Stop_addr_S := SocketManager.Socket[iSocket].Stop_addr;
  Start_addr_S := SocketManager.Socket[iSocket].Start_addr;

  CopyRamToEthMem(@SocketManager.Socket[iSocket].Dest_Mac, TXSTART, 6);
  CopyRamToEthMem(@Settings.eth_mac, TXSTART + 6, 6);
  CopyRamToEthMem(@Settings.eth_ip_addr, TXSTART + 26, 4);
  CopyRamToEthMem(@SocketManager.Socket[iSocket].Dest_Ip, TXSTART + 30, 4);
  WriteToEthMem(TXSTART + 53, $00);

  if SocketManager.Socket[iSocket].RAM_ROM = 0 then
  begin
    BytesToWrite := dword(Stop_addr_S - (Start_addr_S + AddressOffset));
    if BytesToWrite >= MaxPacketDataSize then
      BytesToWrite := MaxPacketDataSize;
    if BytesToWrite <> 0 then
      CopyRamToEthMem_CP((Start_addr_S + AddressOffset), BytesToWrite);
  end else
  if SocketManager.Socket[iSocket].RAM_ROM = 1 then
  begin
    ptf := Start_addr_S + AddressOffset;
    BytesToWrite := 0;
    while dword(ptf) < Stop_addr_S do
    begin
      Eth_PutByte(ptf^);
      ptf := ptf + 1;
      if inc(BytesToWrite) >= MaxPacketDataSize then
        Break;
    end;
    if BytesToWrite = 0 then
      Exit;
  end;

  TCP_WriteToWire_Internal(SocketManager.Socket[iSocket].source_port_S, SocketManager.Socket[iSocket].dest_port_S, BytesToWrite, (SocketManager.Socket[iSocket].SEQ_No_S + AddressOffset), SocketManager.Socket[iSocket].ACK_No_S, TCP_Flag_T);

  Result := BytesToWrite;
end;

//******************************************************************************
//* Name    : TCP_Send_Internal
//* Purpose : Send TCP packets, used by TCP/IP Stack
//  Description:  This is the internal equivalent to the TCP_UserSend function.
//                When the library wants to send a packet it usees this function.
//                It allows other packet flag to be st for other packets than
//                sending data. TCP_UserSend comes through this after it sets
//                up the variables property
//******************************************************************************
function TCP_Send_Internal(TCP_Flag: byte; iSocket: Integer; Fragment: Boolean) : boolean;
var
  AddressOffsetSecondPacket, AddressOffset, iReTransmit, MaxPacketDataSize : word;
  Stop_addr_S, Start_addr_S, seq_buf : dword;
  AddressDelta : dword;
begin
  {$IFDEF LOG_UART}UART1_Write_Text(LF+'Transmitting a TCP Packet................<START>'+LF);{$ENDIF}
  {$IFDEF LOG_UART}
  s1 := 'TCP Tx: TCP Flags: |';
  if TCP_Flag.FIN = 1 then
    s1 := s1 + 'FIN|';
  if TCP_Flag.SYN = 1 then
    s1 := s1 + 'SYN|';
  if TCP_Flag.RST = 1 then
    s1 := s1 + 'RST|';
  if TCP_Flag.PSH = 1 then
    s1 := s1 + 'PSH|';
  if TCP_Flag.ACK = 1 then
    s1 := s1 + 'ACK|';
  if TCP_Flag.URG = 1 then
    s1 := s1 + 'URG|';
  UART1_Write_Text(+s1+LF);
  {$ENDIF}
  
  Fragment := False;
  
  Stop_addr_S := SocketManager.Socket[iSocket].Stop_addr;
  Start_addr_S := SocketManager.Socket[iSocket].Start_addr;

  Result := False;

  iReTransmit := 0;
  while iReTransmit < 3 do
  begin
    AddressOffset := 0;
    AddressOffsetSecondPacket := 0;

    Eth_DoPacket;
    Eth_DoPacket;

    MaxPacketDataSize := 1300;
    seq_buf := SocketManager.Socket[iSocket].SEQ_No_S;
    SocketManager.Socket[iSocket].Wait_ACK := 1;
    AddressDelta := (Stop_addr_S - Start_addr_S);
    
    // If the Data size is not over the max and Fragment is false then NOTHING is sent in this block of code....
    if AddressDelta <= MaxPacketDataSize then
    begin
      if Fragment then                                                          // Force two packets to be sent if enough data is being sent
      begin
        if AddressDelta >= 4 then                                               // If less than 4 databytes then it will be sent in the second call to TCP_Send_LoadAddressesAndDataThenSend_Internal
        begin
          MaxPacketDataSize := AddressDelta div 2;                              // Only send 1/2 of the data byte available in this packet
          AddressOffset := TCP_Send_LoadAddressesAndDataThenSend_Internal(ACK_S, iSocket, AddressOffset, MaxPacketDataSize);
        end;
      end
    end;
    
    // If this is SYN or FIN then it would not have sent any data yet (Fragment MUST be false for these calls)
    // If the Socket is not connected then if the call is not a FIN or SYN then exit without having done anything
    if SocketManager.Socket[iSocket].State_S <> SOCKET_CONNECTED then
      if TCP_Flag.FIN = 0 then
        if TCP_Flag.SYN = 0 then
        begin
          Result := False;
          {$IFDEF LOG_UART}UART1_Write_Text('TCP Tx: The socket is not connected and the request was not a FIN or SYN, we are out of here...'+LF);{$ENDIF}
          {$IFDEF LOG_UART}UART1_Write_Text('Transmitting a TCP Packet...............<END>'+LF+LF);{$ENDIF}
          Exit;
        end;

    MaxPacketDataSize := 1300;
    AddressOffsetSecondPacket := TCP_Send_LoadAddressesAndDataThenSend_Internal(TCP_Flag, iSocket, AddressOffset, MaxPacketDataSize);   // Send the rest of the data bytes, if there are any

    TickCounter10 := 0;
    while TickCounter10 < SocketManager.TCP_Wait do // wait for ack
    begin
      Eth_DoPacket;                                                             // Local spin looking for the ACK reply.  The rub is the ACK may come with a PSH and data that can cause recursion....
      if SocketManager.Socket[iSocket].Wait_ACK = 0 then                        // Not waiting anymore, received
      begin
    //    SocketManager.Socket[iSocket].Wait_ACK := 1;
    //    if seq_buf <> SocketManager.Socket[iSocket].SEQ_No_S then               // The SEQ_No_S is updated in the receive of the ACK, needs to be better for error detection
          Break;
      end;
    end;
    
    if TickCounter10 < SocketManager.TCP_Wait then
      Break;
      
    inc(iReTransmit);
  end;

  if iReTransmit = 3 then
  begin
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Tx: Packets sent 3 times but timed out waiting for the first ACK from the receiver, we are out of here...'+LF);{$ENDIF}
    {$IFDEF LOG_UART}UART1_Write_Text('Transmitting a TCP Packet................<END>'+LF+LF);{$ENDIF}
    Exit;
  end;

  (*
  if TCP_Flag.FIN = 0 then
    if TCP_Flag.SYN = 0 then
      if (seq_buf + AddressOffset + AddressOffsetSecondPacket) <> SocketManager.Socket[iSocket].SEQ_No_S then
      begin
        if (seq_buf + AddressOffset) = SocketManager.Socket[iSocket].SEQ_No_S then
        begin
          SocketManager.Socket[iSocket].Wait_ACK := 1;
          TickCounter10 := 0;
          while TickCounter10 < SocketManager.TCP_Wait do
          begin
            Eth_DoPacket;
            if SocketManager.Socket[iSocket].Wait_ACK = 0 then // received
            begin
              if (seq_buf + AddressOffset + AddressOffsetSecondPacket) <> SocketManager.Socket[iSocket].SEQ_No_S then
                AddressOffsetSecondPacket := 0;
              Break;
            end;
          end;
        end else
        begin
          {$IFDEF LOG_UART}UART1_Write_Text('TCP Tx: The first ACK from the receiver had a messed up Seq Number, we are out of here...'+LF);{$ENDIF}
          {$IFDEF LOG_UART}UART1_Write_Text('Transmitting a TCP Packet................<END>'+LF+LF);{$ENDIF}
          Exit;
        end
      end;
   *)
   
  // Increment the sliding address for the data window
  if (SocketManager.Socket[iSocket].Start_addr = Start_addr_S) and (SocketManager.Socket[iSocket].Stop_addr = Stop_addr_S) then
    SocketManager.Socket[iSocket].Start_addr := SocketManager.Socket[iSocket].Start_addr + AddressOffsetSecondPacket + AddressOffset;

  Result := True;
  {$IFDEF LOG_UART}UART1_Write_Text('Transmitting a TCP Packet................<END>'+LF+LF);{$ENDIF}
end;

//******************************************************************************
//* Name    : TCP_UserSend                                                         *
//* Purpose : Send TCP packet                                                  *
//******************************************************************************
function TCP_UserSend(var ip_t1 : TipHeader; dest_port_S1, len_data1 : word; var data_tcp2 : array[1472] of byte) : boolean;
var 
  iSocket : Integer;
begin
  iSocket := TCP_Find_Connection(ip_t1, dest_port_S1);

  if iSocket < 0 then // no sockets available
  begin
    Result := False;
    Exit;
  end;
  
  SocketManager.Socket[iSocket].RAM_ROM := 0;
  SocketManager.Socket[iSocket].Start_addr := @data_tcp2;
  SocketManager.Socket[iSocket].Stop_addr := @data_tcp2 + len_data1;
  Result := TCP_Send_Internal(PSH_S + ACK_S, iSocket, True);
  if Result = False then
    begin
      SocketManager.Socket[iSocket].State_S := SOCKET_DISCONNECTED;
      SocketManager.Socket[iSocket].Keep_Alive := False;
    end;
end;

//******************************************************************************
//* Name    : TCP_WriteToWire_Internal                                                      *
//* Purpose : Send TCP packet, used by library                                 *
//  Descriptions:
//     The data has already been written to the ENC by the time this is called
//******************************************************************************
procedure TCP_WriteToWire_Internal(source_portT, dest_portT, PacketLenT : word; SEQ_NoT, ACK_NoT : dWord; TCP_Flag : byte);
var
  TotalLen : word;
  Align : byte;
  CkSum_TCP, CkSum_IP: Word;
begin

  {$IFDEF LOG_UART}
  s1 := '|';
  if TCP_Flag.FIN = 1 then
    s1 := s1 + 'FIN|';
  if TCP_Flag.SYN = 1 then
    s1 := s1 + 'SYN|';
  if TCP_Flag.RST = 1 then
    s1 := s1 + 'RST|';
  if TCP_Flag.PSH = 1 then
    s1 := s1 + 'PSH|';
  if TCP_Flag.ACK = 1 then
    s1 := s1 + 'ACK|';
  if TCP_Flag.URG = 1 then
    s1 := s1 + 'URG|';
  UART1_Write_Text('TCP Write to wire with Flags: '+s1+LF);
  {$ENDIF}

  WriteToEthMem(TXSTART+12,$08);    // Phy Layer Protocol HI
  Eth_PutByte($00);                 // Phy Layer Protocol LO
  Eth_PutByte($45);                 // Version/IHL
  Eth_PutByte($00);                 // DCP/ECN
  TotalLen := 40 + PacketLenT;      // Does not count the physical layer MAC addresses or Protocol type
  Eth_PutByte(Hi(TotalLen));        // Total Length HI
  Eth_PutByte(Lo(TotalLen));        // Total Length LO
  inc(TCP_IdentificationCounter);
  Eth_PutByte(Hi(TCP_IdentificationCounter));     // Identification HI
  Eth_PutByte(Lo(TCP_IdentificationCounter));     // Identification LO
  Eth_PutByte($40);                 // Fragment Flags (DF, don't fragment) / Fragment Offset upper bits
  Eth_PutByte($00);                 // Fragment Offset Lo
  Eth_PutByte($80);                 // Time to Live in seconds (128 seconds)
  Eth_PutByte($06);                 // Protocol = TCP
  Eth_PutByte($00);                 // Checksum HI
  Eth_PutByte($00);                 // Checksum LO
  CkSum_IP := Eth_Cksum(TXSTART+14, 20); // Checksum the entire IP Header of 20 bytes
  WriteToEthMem(TXSTART+24 ,Hi(CkSum_IP)); // Write check sum to Header Checksum offset in Header (with 0 for the Checksum we calculated)
  Eth_PutByte(Lo(CkSum_IP));
  WriteToEthMem(TXSTART+34, Hi(source_portT));  // TCP Source Port Hi
  Eth_PutByte(Lo(source_portT));               // Lo
  Eth_PutByte(Hi(dest_portT));                 // TCP Dest Port Hi
  Eth_PutByte(Lo(dest_portT));                 // Lo
  CopyRamToEthMem_Inv(@SEQ_NoT, TXSTART+38, 4);
  CopyRamToEthMem_Inv(@ACK_NoT, TXSTART+42, 4);
  if TCP_Flag = $12 then                      // if [SYN, ACK] then
    WriteToEthMem(TXSTART+46, $70)             // (upper nibble = 7 - 32 bit block or 8 bytes of options added)
  else
    WriteToEthMem(TXSTART+46, $50);            // (upper nibble = 5 so no options)

  WriteToEthMem(TXSTART+47, TCP_Flag);       // Flags
  Window := 65200;
  Eth_PutByte(Hi(Window));                    // Window HI
  Eth_PutByte(Lo(Window));                    // Window LO

  WriteToEthMem(TXSTART+50, $00);             // TCP checksum HI zeroed out
  Eth_PutByte($00);                           // TCP checksum LO zeroed out
  Eth_PutByte($00);                           // TCP Urgent Ptr Hi zeroed out
  Eth_PutByte($00);                           // TCP Urgent Ptr Lo zeroed out
  WriteToEthMem(TXSTART+14+TotalLen, $00);    // The 2 data bytes PAST the data write 0
  Eth_PutByte($00);
  Align := TotalLen and $01;
  CopyEthMemToEthMem(TXSTART+26, TXSTART+14+TotalLen+Align, 8);
  WriteToEthMem(TXSTART+22+TotalLen+Align, $00);
  Eth_PutByte($06);
  TotalLen := TotalLen - 20;
  Eth_PutByte(Hi(TotalLen));
  Eth_PutByte(Lo(TotalLen));
  CkSum_TCP := Eth_Cksum(TXSTART+34, 12+TotalLen+Align);
  WriteToEthMem(TXSTART+50, Hi(CkSum_TCP));
  Eth_PutByte(Lo(CkSum_TCP));
  Eth_Send(34+TotalLen);                                                        // Blocks until sent
end;

//******************************************************************************
//* Name    : TCP_Process_Internal                                                   *
//* Purpose : Process incomming TCP packets                                    *
//******************************************************************************
procedure TCP_Process_Internal(EthMemAddressRequest: Word);
var
  PacketLen, source_port2, dest_port2, len_ack : word;
  iSocket: Integer;
  Align, TCP_FlagReceive, TCP_FlagSend : byte;
  dest_mac2 : TmacHeader;
  dest_ip_addr: TipHeader;
  CkSum_TCP: word;
  CkSum_IP: word;
  len_data: word;
label
  next_phs;

begin
  source_port2 := 0;
  CkSum_IP := 0;
  CkSum_TCP := 0;
  len_data := 0;
  dest_port2 := 0;
  Hi(source_port2) := ReadFromEthMem(EthMemAddressRequest+36);
  Lo(source_port2) := Eth_GetByte;
  if Search_Port(TCP, source_port2) = False then
    begin
      if Settings.Policy = POLICY_REJECT then
        Common_Reject_Internal(EthMemAddressRequest);
      Exit;
    end;

  Hi(CkSum_IP) :=  ReadFromEthMem(EthMemAddressRequest+24);
  Lo(CkSum_IP) :=  Eth_GetByte;
  Hi(CkSum_TCP) := ReadFromEthMem(EthMemAddressRequest+50);
  Lo(CkSum_TCP) := Eth_GetByte;
  Hi(len_data) := ReadFromEthMem(EthMemAddressRequest+16);
  Lo(len_data) := Eth_GetByte;
  len_ack := len_data;
  PacketLen := len_data - 40;
  CopyEthMemToEthMem(EthMemAddressRequest,TXSTART,len_data+14);
  WriteToEthMem(TXSTART+24,$00);
  Eth_PutByte($00);
  if cksum_ip <> Eth_Cksum(TXSTART+14,20) then
    Exit;
  Align := len_data and $01;
  WriteToEthMem(TXSTART+50,$00);
  Eth_PutByte($00);
  WriteToEthMem(TXSTART+len_data+14,$00);
  Eth_PutByte($00);
  CopyEthMemToEthMem(TXSTART+26,TXSTART+len_data+Align+14,8);
  WriteToEthMem(TXSTART+len_data+Align+22,$00);
  Eth_PutByte($06);
  len_data := len_data - 20;
  Eth_PutByte(Hi(len_data));
  Eth_PutByte(Lo(len_data));
  if CkSum_TCP <> Eth_Cksum(TXSTART+34,len_data + Align + 12) then
    Exit;
  Hi(dest_port2) := ReadFromEthMem(EthMemAddressRequest+34);
  Lo(dest_port2) := Eth_GetByte;
  TCP_FlagReceive := ReadFromEthMem(EthMemAddressRequest + 47);
  CopyEthMemToRam_Inv(EthMemAddressRequest + 38,@SEQ_NoR,4);
  CopyEthMemToRam_Inv(EthMemAddressRequest + 42,@ACK_NoR,4);
  Hi(Window) := ReadFromEthMem(EthMemAddressRequest+48);
  Lo(Window) := Eth_GetByte;
  CopyEthMemToRam(EthMemAddressRequest + 26,@dest_ip_addr,4);
  CopyEthMemToRam(EthMemAddressRequest + 6, @dest_mac2,6);

  {$IFDEF LOG_UART}
  s1 := 'TCP Flags: |';
  if TCP_FlagReceive.FIN = 1 then
    s1 := s1 + 'FIN|';
  if TCP_FlagReceive.SYN = 1 then
    s1 := s1 + 'SYN|';
  if TCP_FlagReceive.RST = 1 then
    s1 := s1 + 'RST|';
  if TCP_FlagReceive.PSH = 1 then
    s1 := s1 + 'PSH|';
  if TCP_FlagReceive.ACK = 1 then
    s1 := s1 + 'ACK|';
  if TCP_FlagReceive.URG = 1 then
    s1 := s1 + 'URG|';
  UART1_Write_Text(+s1+LF);
  {$ENDIF}

  // Handle RST..................................................................
  if TCP_FlagReceive.RST = 1 then
  begin
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling RST'+LF);{$ENDIF}
    iSocket := TCP_Find_Any_Connection(dest_ip_addr, dest_port2);
    if iSocket >= 0 then
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Found the Socket to Reset'+LF);{$ENDIF}
      SocketManager.Socket[iSocket].State_S := SOCKET_DISCONNECTED; // disconnect
      SocketManager.Socket[iSocket].Keep_Alive := False;
      SocketManager.Socket[iSocket].Wait_ACK := 0;
    end;

    {$IFDEF LOG_UART}
    if iSocket < 0 then
      UART1_Write_Text('TCP Rx: Count not find the Socket to Reset'+LF);
    {$ENDIF}
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a RST, done we are out of here'+LF);{$ENDIF}
    
    Exit;
  end;
  // Handle RST..................................................................

  // Handle FIN..................................................................
  if PacketLen = 0 then
  begin
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Packet Length = 0 Handling....'+LF);{$ENDIF}
    
    if TCP_FlagReceive.FIN = 1 then
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling FIN'+LF);{$ENDIF}
      iSocket := TCP_Find_Any_Connection(dest_ip_addr, dest_port2);
      if iSocket >= 0 then
      begin
        {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling FIN and found Socket'+LF);{$ENDIF}
        SocketManager.Socket[iSocket].State_S := SOCKET_DISCONNECTED; // disconnect
        SocketManager.Socket[iSocket].Keep_Alive := False;
        SocketManager.Socket[iSocket].Wait_ACK := 0;
      end;

      {$IFDEF LOG_UART}
      if iSocket < 0 then
        UART1_Write_Text('TCP Rx: Handling FIN and could not find Socket'+LF);
      {$ENDIF}

      if TCP_FlagReceive.RST = 1 then    // This should have been detected above and there is no way this block of code should ever get called
      begin
        {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling FIN with a RST we are out of here'+LF);{$ENDIF}
        Exit; // Note that RST packets are not acknowledged.
      end;

      SEQ_No := ACK_NoR;
      ACK_No := SEQ_NoR + 1;

      if TCP_FlagReceive.ACK = 1 then
      begin
        {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Hanlding FIN with an ACK, asking for a 3 step finish (FIN+ACK), just send and ACK'+LF);{$ENDIF}
        TCP_FlagSend := ACK_S                 // FIN + ACK, response only with ACK
      end else 
      begin
        if TCP_FlagReceive.FIN = 1 then      // We have already established this is true here...
        begin
          {$IFDEF TRACE_TCP_RX}UART1_Write_Text('TCP Rx: Handling FIN by itself, normal dual question/answer finish (FIN+ACK) send an ACK+FIN to ACK the FIN and send our own FIN'+LF);{$ENDIF}
          TCP_FlagSend := ACK_S + FIN_S;   // FIN response with FIN + ACK
        end
      end;
      CopyEthMemToEthMem(EthMemAddressRequest + 6, TXSTART, 6);
      CopyRamToEthMem(@Settings.eth_mac, TXSTART + 6, 6);
      CopyRamToEthMem(@Settings.eth_ip_addr, TXSTART + 26, 4);
      CopyEthMemToEthMem(EthMemAddressRequest + 26, TXSTART + 30, 4);
      TCP_WriteToWire_Internal(source_port2, dest_port2, 0, SEQ_No, ACK_No, TCP_FlagSend);
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling FIN done, we are out of here'+LF);{$ENDIF}
      Exit;
    end;
  end;
  // Handle FIN..................................................................

  // Handle SYN..................................................................
  if TCP_FlagReceive.SYN = 1 then // SYN
  begin
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN'+LF);{$ENDIF}
    iSocket := TCP_Find_Any_Connection(dest_ip_addr, dest_port2);
    if iSocket < 0 then
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN, could not find allocated Socket'+LF);{$ENDIF}
      iSocket := TCP_Find_Free_Socket;
    end else
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN, found allocated Socket'+LF);{$ENDIF}
    end;

    if iSocket < 0 then
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN, could not find free Socket we are out of here'+LF);{$ENDIF}
      Exit;
    end;

    SEQ_No := ACK_NoR;
    ACK_No := SEQ_NoR + 1;

    if TCP_FlagReceive.ACK = 1 then // If we opened the connection and the other node has replied with a SYN+ACK
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN with and ACK, we opened the connection and the other node has replied with a SYN+ACK, our connection is now OPEN. reply with an ACK'+LF);{$ENDIF}
      TCP_FlagSend := ACK_S;
      Align := 0;
      SocketManager.Socket[iSocket].State_S := SOCKET_CONNECTED; // Connected
    end else
    begin                           // If we are listening and a client asks to open a connection by sending just a ACK
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a SYN by itself, we are listening and a client asks to open a connection by sending just a ACK, reply with a SYN+ACK'+LF);{$ENDIF}
      TCP_FlagSend := SYN_S + ACK_S;
      Align := 8;
      SocketManager.Socket[iSocket].State_S := SOCKET_WAITING_FOR_CONNECTION; // Wait for Connection
    end;

    SocketManager.Socket[iSocket].Wait_Ack := 0;
    SocketManager.Socket[iSocket].SEQ_No_S := SEQ_No;
    SocketManager.Socket[iSocket].ACK_No_S := ACK_No;
    SocketManager.Socket[iSocket].dest_port_S := dest_port2;
    SocketManager.Socket[iSocket].source_port_S := source_port2;
    Mem_Cpy(@SocketManager.Socket[iSocket].Dest_Ip, @dest_ip_addr, 4);
    Mem_Cpy(@SocketManager.Socket[iSocket].Dest_Mac, @dest_mac2, 6);
    SocketManager.Socket[iSocket].Exp_Time := 0;
    SocketManager.Socket[iSocket].Start_addr := 0;
    SocketManager.Socket[iSocket].Stop_addr := 0;
    SocketManager.Socket[iSocket].Keep_Alive := True; // Keep Alive

    CopyEthMemToEthMem(EthMemAddressRequest + 6, TXSTART, 6);
    CopyRamToEthMem(@Settings.eth_mac, TXSTART + 6, 6);
    CopyRamToEthMem(@Settings.eth_ip_addr, TXSTART + 26, 4);
    CopyEthMemToEthMem(EthMemAddressRequest + 26, TXSTART + 30, 4);
    TCP_WriteToWire_Internal(source_port2, dest_port2, Align, SEQ_No, ACK_No, TCP_FlagSend);
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling SYN done, we are out of here'+LF);{$ENDIF}
    Exit;
  end;
  // Handle SYN..................................................................

  // Handle ACK..................................................................
  if TCP_FlagReceive.ACK = 1 then // ACK
  begin
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx ACK Handling.....'+LF);{$ENDIF}
    iSocket := TCP_Find_Any_Connection(dest_ip_addr, dest_port2);

    if iSocket >= 0 then
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a ACK, found allocated Socket'+LF);{$ENDIF}
      if (SocketManager.Socket[iSocket].State_S = SOCKET_WAITING_FOR_CONNECTION) {or (SocketManager.Socket[iSocket].Wait_ACK <> 0)} then
      begin
        {$IFDEF LOG_UART}
        if (SocketManager.Socket[iSocket].State_S = SOCKET_WAITING_FOR_CONNECTION) then
          UART1_Write_Text('TCP Rx: Handling a ACK, this was an ACK in response to a SYN (us) - SYN+ACK (them), connection is complete'+LF)
        else
          UART1_Write_Text('TCP Rx: Handling a ACK, this was an ACK in response to general Wait_ACK'+LF);
        {$ENDIF}
        SocketManager.Socket[iSocket].State_S := SOCKET_CONNECTED; // Connected
        SocketManager.Socket[iSocket].Wait_ACK := 0;
        SocketManager.Socket[iSocket].ACK_No_S := SEQ_NoR;
        SocketManager.Socket[iSocket].SEQ_No_S := ACK_NoR;
        SocketManager.Socket[iSocket].Exp_Time := 0; // keep Alive
        Exit;
      end else
      begin                                       
        {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a ACK, this is a data packet being sent to us'+LF);{$ENDIF}

        SEQ_No := ACK_NoR;
        ACK_No := SEQ_NoR + PacketLen;
        TCP_FlagSend := ACK_S;

        SocketManager.Socket[iSocket].Wait_ACK := 0;
        SocketManager.Socket[iSocket].SEQ_No_S := SEQ_No;
        SocketManager.Socket[iSocket].ACK_No_S := ACK_No;
        SocketManager.Socket[iSocket].Exp_Time := 0; // keep Alive
        
        if (SocketManager.Socket[iSocket].Wait_ACK <> 0) then
        begin
          SocketManager.Socket[iSocket].dest_port_S := dest_port2;
          SocketManager.Socket[iSocket].source_port_S := source_port2;
          CopyEthMemToEthMem(EthMemAddressRequest + 6, TXSTART, 6);
          CopyRamToEthMem(@Settings.eth_mac, TXSTART + 6, 6);
          CopyRamToEthMem(@Settings.eth_ip_addr, TXSTART + 26, 4);
          CopyEthMemToEthMem(EthMemAddressRequest + 26, TXSTART + 30, 4);
          TCP_WriteToWire_Internal(source_port2, dest_port2, 0, SEQ_No, ACK_No, TCP_FlagSend);      // Send an ACK for this packet of data
        end;
        
        Align := ReadFromEthMem(EthMemAddressRequest + 53); // dummy read puts rx pointer to start of TCP data.
        if PacketLen <> 0 then
        begin
          CopyEthMem_CPToRam(word(@SocketManager.Socket[iSocket].TCP_buf), PacketLen);
          SocketManager.Socket[iSocket].Start_addr := 0;
          SocketManager.Socket[iSocket].Stop_addr := PacketLen;
          Eth_UserTCP(dest_ip_addr, source_port2, dest_port2, PacketLen, iSocket);
          {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a ACK, data packet, sent to Eth_UserTCP'+LF);{$ENDIF}
        end else
        begin
          {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a ACK, data packet but data length was 0???'+LF);{$ENDIF}
        end
      end;
    end else
    begin
      {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx ACK Handling Socket not found'+LF);{$ENDIF}
    end;
    {$IFDEF LOG_UART}UART1_Write_Text('TCP Rx: Handling a ACK, done we are out of here'+LF);{$ENDIF}
  end;
  // Handle ACK..................................................................
end;

end.